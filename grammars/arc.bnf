<color> ::= <color_const> |
            <color> if <bool> else <color> |
            mode(m, <sel>) |
            first(m, <sel>)
            
# selections: lists of relative points
<sel> ::= [<point_list>] |
          rectangle(<point>, <point>) |
          neighborhood(<point>, <digit>)
<point_list> ::= <point> | <point>, <point_list>
<point> ::= (<num>+x, <num>+y)

<bool> ::= <bool_const> |
           <bool_exp> |
           all([<bool_list>]) |
           any([<bool_list>]) |
           (not <bool>)
<bool_list> ::= <bool> | <bool>, <bool_list>
           
<bool_const> ::= True | False
<bool_exp> ::= contains(m, <sel>, <color>) |
               (<color> == <color>) |
               selection_equals(m, <sel>, <sel>) |
               any_hidden(m, <sel>) | 
               all_hidden(m, <sel>)
               
# one-digit numbers
<num> ::= 0 | <digit> | -<digit>
<digit> ::= 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9
<color_const> ::= None | 0 | <digit>
